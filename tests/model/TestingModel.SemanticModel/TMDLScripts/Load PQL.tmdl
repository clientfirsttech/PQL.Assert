createOrReplace
	/// DAX Assertion Library Functions
	/// Publisher: PQL (Power Query Lint)
	/// These functions provide a standardized way to write unit tests for DAX models
	/// Basic Assertions - PQL.Assert namespace
	function 'PQL.Assert.ShouldBeTrue' =
			(testName : STRING, actualCondition : BOOLEAN) =>
				VAR _Passed = actualCondition = TRUE
				RETURN ROW(
					"TestName", testName,
					"Expected", "TRUE",
					"Actual", IF(actualCondition, "TRUE", "FALSE"),
					"Passed", _Passed
				)

	function 'PQL.Assert.ShouldBeFalse' =
			(testName : STRING, actualCondition : BOOLEAN) =>
				VAR _Passed = actualCondition = FALSE
				RETURN ROW(
					"TestName", testName,
					"Expected", "FALSE",
					"Actual", IF(actualCondition, "TRUE", "FALSE"),
					"Passed", _Passed
				)

	/// Asserts that a value is BLANK (DAX null).
	function 'PQL.Assert.ShouldBeBlank' =
			(testName : STRING, actualValue : VARIANT) =>
				VAR _Passed = ISBLANK(actualValue)
				RETURN ROW(
					"TestName", testName,
					"Expected", "BLANK",
					"Actual", IF(ISBLANK(actualValue), "BLANK", "NOT BLANK"),
					"Passed", _Passed
				)

	/// Asserts that a value is not BLANK. Accepts both simple values and expressions.
	function 'PQL.Assert.ShouldNotBeBlank' =
			(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
				VAR _ActualResult = actualValue
				VAR _Passed = NOT ISBLANK(_ActualResult)
				RETURN ROW(
					"TestName", testName,
					"Expected", "NOT BLANK",
					"Actual", IF(ISBLANK(_ActualResult), "BLANK", "NOT BLANK"),
					"Passed", _Passed
				)

	/// Asserts that a value is BLANK or an empty string.
	function 'PQL.Assert.ShouldBeBlankOrEmpty' =
			(testName : STRING, actualValue : VARIANT) =>
				VAR _IsBlankOrEmpty = ISBLANK(actualValue) || actualValue = ""
				RETURN ROW(
					"TestName", testName,
					"Expected", "BLANK OR EMPTY",
					"Actual", IF(ISBLANK(actualValue), "BLANK", IF(actualValue = "", "EMPTY", "HAS VALUE")),
					"Passed", _IsBlankOrEmpty
				)

	/// Asserts that a value is not BLANK and not an empty string (has actual content).
	function 'PQL.Assert.ShouldNotBeBlankOrEmpty' =
			(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
				VAR _ActualResult = actualValue
				VAR _HasContent = NOT ISBLANK(_ActualResult) && _ActualResult <> ""
				RETURN ROW(
					"TestName", testName,
					"Expected", "NOT BLANK AND NOT EMPTY",
					"Actual", IF(ISBLANK(_ActualResult), "BLANK", IF(_ActualResult = "", "EMPTY", "HAS VALUE")),
					"Passed", _HasContent
				)

	function 'PQL.Assert.ShouldEqual' =
			(testName : STRING, expected : VARIANT, actual : VARIANT) =>
				VAR _Passed = expected = actual
				RETURN ROW(
					"TestName", testName,
					"Expected", FORMAT(expected, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	function 'PQL.Assert.ShouldNotEqual' =
			(testName : STRING, notExpected : VARIANT, actual : VARIANT) =>
				VAR _Passed = notExpected <> actual
				RETURN ROW(
					"TestName", testName,
					"Expected", "NOT " & FORMAT(notExpected, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	/// Asserts that two strings are exactly equal (case-sensitive comparison).
	function 'PQL.Assert.ShouldEqualExactly' =
			(testName : STRING, expected : STRING, actual : STRING) =>
				VAR _Passed = EXACT(expected, actual)
				RETURN ROW(
					"TestName", testName,
					"Expected", expected,
					"Actual", actual,
					"Passed", _Passed
				)

	function 'PQL.Assert.ShouldBeGreaterThan' =
			(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
				VAR _Passed = actual > threshold
				RETURN ROW(
					"TestName", testName,
					"Expected", "> " & FORMAT(threshold, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	function 'PQL.Assert.ShouldBeLessThan' =
			(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
				VAR _Passed = actual < threshold
				RETURN ROW(
					"TestName", testName,
					"Expected", "< " & FORMAT(threshold, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	/// Asserts that a number is greater than or equal to an expected value.
	function 'PQL.Assert.ShouldBeGreaterOrEqual' =
			(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
				VAR _Passed = actual >= threshold
				RETURN ROW(
					"TestName", testName,
					"Expected", ">= " & FORMAT(threshold, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	/// Asserts that a number is less than or equal to an expected value.
	function 'PQL.Assert.ShouldBeLessOrEqual' =
			(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
				VAR _Passed = actual <= threshold
				RETURN ROW(
					"TestName", testName,
					"Expected", "<= " & FORMAT(threshold, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	function 'PQL.Assert.ShouldBeBetween' =
			(testName : STRING, lowerBound : DOUBLE, upperBound : DOUBLE, actual : DOUBLE) =>
				VAR _Passed = actual >= lowerBound && actual <= upperBound
				RETURN ROW(
					"TestName", testName,
					"Expected", FORMAT(lowerBound, "") & " <= value <= " & FORMAT(upperBound, ""),
					"Actual", FORMAT(actual, ""),
					"Passed", _Passed
				)

	/// String Assertions - PQL.Assert namespace
	/// Asserts that a string starts with the specified prefix (case-insensitive).
	function 'PQL.Assert.ShouldStartWith' =
			(testName : STRING, prefix : STRING, actual : STRING) =>
				VAR _Passed = LEFT(UPPER(actual), LEN(prefix)) = UPPER(prefix)
				RETURN ROW(
					"TestName", testName,
					"Expected", "Starts with '" & prefix & "'",
					"Actual", actual,
					"Passed", _Passed
				)

	/// Asserts that a string ends with the specified suffix (case-insensitive).
	function 'PQL.Assert.ShouldEndWith' =
			(testName : STRING, suffix : STRING, actual : STRING) =>
				VAR _Passed = RIGHT(UPPER(actual), LEN(suffix)) = UPPER(suffix)
				RETURN ROW(
					"TestName", testName,
					"Expected", "Ends with '" & suffix & "'",
					"Actual", actual,
					"Passed", _Passed
				)

	/// Asserts that a string contains the specified substring (case-insensitive).
	function 'PQL.Assert.ShouldContainString' =
			(testName : STRING, substring : STRING, actual : STRING) =>
				VAR _Passed = CONTAINSSTRING(actual, substring)
				RETURN ROW(
					"TestName", testName,
					"Expected", "Contains '" & substring & "'",
					"Actual", actual,
					"Passed", _Passed
				)

	/// Asserts that a string matches a pattern (case-insensitive).
	function 'PQL.Assert.ShouldMatch' =
			(testName : STRING, pattern : STRING, actual : STRING) =>
				VAR _Passed = CONTAINSSTRING(actual, pattern)
				RETURN ROW(
					"TestName", testName,
					"Expected", "Matches '" & pattern & "'",
					"Actual", actual,
					"Passed", _Passed
				)

	/// Col Assertions - PQL.Assert.Col namespace
	/// Asserts that a column contains at least one non-blank value. Pass column reference directly.
	function 'PQL.Assert.Col.ShouldNotBeBlank' =
			(testName : STRING, columnRef : ANYREF EXPR) =>
				VAR _NonBlankCount = DISTINCTCOUNTNOBLANK(columnRef)
				VAR _Passed = _NonBlankCount > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Column has non-blank values",
					"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonBlankCount, "0") & " distinct)", "ALL BLANK"),
					"Passed", _Passed
				)

	/// Asserts that all values in a column are BLANK or empty string.
	function 'PQL.Assert.Col.ShouldBeBlankOrEmpty' =
			(testName : STRING, columnRef : ANYREF EXPR) =>
				VAR _NonEmptyCount = COUNTAX(
					VALUES(columnRef),
					IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
				)
				VAR _Passed = _NonEmptyCount = 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Column is all BLANK or empty",
					"Actual", IF(_Passed, "ALL BLANK/EMPTY", "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)"),
					"Passed", _Passed
				)

	/// Asserts that a column contains at least one value that is not BLANK and not empty string.
	function 'PQL.Assert.Col.ShouldNotBeBlankOrEmpty' =
			(testName : STRING, columnRef : ANYREF EXPR) =>
				VAR _NonEmptyCount = COUNTAX(
					VALUES(columnRef),
					IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
				)
				VAR _Passed = _NonEmptyCount > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Column has non-blank, non-empty values",
					"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)", "ALL BLANK/EMPTY"),
					"Passed", _Passed
				)

	/// Asserts that all values in a column are unique (no duplicates).
	function 'PQL.Assert.Col.ShouldBeDistinct' =
			(testName : STRING, columnRef : ANYREF EXPR) =>
				VAR _DistinctCount = DISTINCTCOUNT(columnRef)
				VAR _TotalCount = COUNTROWS(VALUES(columnRef))
				VAR _Passed = _DistinctCount = _TotalCount
				RETURN ROW(
					"TestName", testName,
					"Expected", "All values distinct",
					"Actual", IF(_Passed, "ALL DISTINCT (" & FORMAT(_TotalCount, "0") & " rows)", "DUPLICATES FOUND (" & FORMAT(_DistinctCount, "0") & " distinct of " & FORMAT(_TotalCount, "0") & " rows)"),
					"Passed", _Passed
				)

	function 'PQL.Assert.Col.ShouldExist' =
			(testName : STRING, tableName : STRING, columnName : STRING) =>
				VAR _Columns = INFO.VIEW.COLUMNS()
				VAR _Found = COUNTROWS(FILTER(_Columns, [Table] = tableName && [Name] = columnName)) > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Column '" & tableName & "[" & columnName & "]' exists",
					"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
					"Passed", _Found
				)

	/// Table Assertions - PQL.Assert.Table namespace
	/// Asserts that a table has at least one row.
	function 'PQL.Assert.Tbl.ShouldHaveRows' =
			(testName : STRING, tableRef : TABLE EXPR) =>
				VAR _RowCount = COUNTROWS(tableRef)
				VAR _Passed = _RowCount > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Table has rows",
					"Actual", IF(_Passed, "HAS ROWS (" & FORMAT(_RowCount, "0") & ")", "EMPTY (0 rows)"),
					"Passed", _Passed
				)

	/// Asserts that a table has exactly the expected number of rows.
	function 'PQL.Assert.Tbl.ShouldHaveRowCount' =
			(testName : STRING, tableRef : TABLE EXPR, expectedRowCount : INT64) =>
				VAR _RowCount = COUNTROWS(tableRef)
				VAR _Passed = _RowCount = expectedRowCount
				RETURN ROW(
					"TestName", testName,
					"Expected", FORMAT(expectedRowCount, "0") & " rows",
					"Actual", FORMAT(_RowCount, "0") & " rows",
					"Passed", _Passed
				)

	function 'PQL.Assert.Tbl.ShouldHaveMoreRowsThan' =
			(testName : STRING, threshold : INT64, tableToCheck : TABLE) =>
				VAR _RowCount = COUNTROWS(tableToCheck)
				VAR _Passed = _RowCount > threshold
				RETURN ROW(
					"TestName", testName,
					"Expected", "> " & FORMAT(threshold, "0"),
					"Actual", FORMAT(_RowCount, "0"),
					"Passed", _Passed
				)

	function 'PQL.Assert.Tbl.ShouldExist' =
			(testName : STRING, tableName : STRING) =>
				VAR _Tables = INFO.VIEW.TABLES()
				VAR _Found = COUNTROWS(FILTER(_Tables, [Name] = tableName)) > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Table '" & tableName & "' exists",
					"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
					"Passed", _Found
				)

	/// Relationship Assertions - PQL.Assert.Relationship namespace
	function 'PQL.Assert.Relationship.ShouldExist' =
			(testName : STRING, fromTable : STRING, fromColumn : STRING, toTable : STRING, toColumn : STRING) =>
				VAR _Relationships = INFO.VIEW.RELATIONSHIPS()
				VAR _Found = COUNTROWS(FILTER(_Relationships,
					[FromTable] = fromTable && [FromColumn] = fromColumn &&
					[ToTable] = toTable && [ToColumn] = toColumn)) > 0
				RETURN ROW(
					"TestName", testName,
					"Expected", "Relationship '" & fromTable & "[" & fromColumn & "] -> " & toTable & "[" & toColumn & "]' exists",
					"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
					"Passed", _Found
				)

