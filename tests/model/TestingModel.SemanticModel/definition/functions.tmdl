/// DAX Assertion Library Functions
/// Publisher: PQL (Power Query Lint)
/// These functions provide a standardized way to write unit tests for DAX models
/// Basic Assertions - PQL.Assert namespace
function 'PQL.Assert.ShouldBeTrue' =
		(testName : STRING, actualCondition : BOOLEAN) =>
			VAR _Passed = actualCondition = TRUE
			RETURN ROW(
				"TestName", testName,
				"Expected", "TRUE",
				"Actual", IF(actualCondition, "TRUE", "FALSE"),
				"Passed", _Passed
			)
	lineageTag: 0bdac25f-f8d5-4417-8b66-36000615491b

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldBeFalse' =
		(testName : STRING, actualCondition : BOOLEAN) =>
			VAR _Passed = actualCondition = FALSE
			RETURN ROW(
				"TestName", testName,
				"Expected", "FALSE",
				"Actual", IF(actualCondition, "TRUE", "FALSE"),
				"Passed", _Passed
			)
	lineageTag: a5c89d96-e7ad-43b1-834f-6ef3d7a28b43

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is NULL (DAX BLANK()).
function 'PQL.Assert.ShouldBeNull' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = ISBLANK(actualValue)
			RETURN ROW(
				"TestName", testName,
				"Expected", "NULL",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 7717c498-0d22-4a0c-9dea-54c669f8a546

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is not NULL (not DAX BLANK()). Accepts both simple values and expressions.
function 'PQL.Assert.ShouldNotBeNull' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = NOT ISBLANK(_ActualResult)
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT NULL",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 8e434609-6e12-49c5-9ae5-36d18f1e37a2

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is BLANK (empty string "").
function 'PQL.Assert.ShouldBeBlank' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = NOT ISBLANK(actualValue) && actualValue = ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "BLANK (empty string)",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 5217f758-5140-43b2-aa25-e8b62289c75c

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is not BLANK (not empty string "").
function 'PQL.Assert.ShouldNotBeBlank' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = ISBLANK(_ActualResult) || _ActualResult <> ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT BLANK (not empty string)",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: e496554f-cb09-40ff-aa07-e5f4f9fc7335

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is NULL or BLANK (BLANK() or empty string "").
function 'PQL.Assert.ShouldBeNullOrBlank' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = ISBLANK(actualValue) || actualValue = ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NULL OR BLANK",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 2de58a1d-98eb-488b-a8a2-35cf656a65d4

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a value is not NULL and not BLANK (has actual content).
function 'PQL.Assert.ShouldNotBeNullOrBlank' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = NOT ISBLANK(_ActualResult) && _ActualResult <> ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT NULL AND NOT BLANK",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 9f54ff3b-e517-450d-bf0d-b769d2f35e93

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldEqual' =
		(testName : STRING, expected : VARIANT, actual : VARIANT) =>
			VAR _Passed = expected = actual
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(expected, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: f99f86c2-bc06-44d9-ba32-71867e17d0f2

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldNotEqual' =
		(testName : STRING, notExpected : VARIANT, actual : VARIANT) =>
			VAR _Passed = notExpected <> actual
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT " & FORMAT(notExpected, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 92f55d17-6f56-49ac-a707-6eb8d836b3b1

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that two strings are exactly equal (case-sensitive comparison).
function 'PQL.Assert.ShouldEqualExactly' =
		(testName : STRING, expected : STRING, actual : STRING) =>
			VAR _Passed = EXACT(expected, actual)
			RETURN ROW(
				"TestName", testName,
				"Expected", expected,
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: fe447cb7-1645-43f4-bbd4-94d36572597e

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldBeGreaterThan' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual > threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "> " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: bc47d3f4-223c-4c55-b77b-48d17f660e1f

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldBeLessThan' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual < threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "< " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 669bd7ec-cc4d-4178-90fb-4cf57c5246dd

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a number is greater than or equal to an expected value.
function 'PQL.Assert.ShouldBeGreaterOrEqual' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual >= threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", ">= " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: cf60c15d-2a31-45bc-a72f-cc5270cfc19b

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a number is less than or equal to an expected value.
function 'PQL.Assert.ShouldBeLessOrEqual' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual <= threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "<= " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 31671d5c-ab8a-424b-803e-7a45db5ced39

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.ShouldBeBetween' =
		(testName : STRING, lowerBound : DOUBLE, upperBound : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual >= lowerBound && actual <= upperBound
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(lowerBound, "") & " <= value <= " & FORMAT(upperBound, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: ba5cd3c0-9f00-4898-bae9-715a6a22b43a

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// String Assertions - PQL.Assert namespace
/// Asserts that a string starts with the specified prefix (case-insensitive).
function 'PQL.Assert.ShouldStartWith' =
		(testName : STRING, prefix : STRING, actual : STRING) =>
			VAR _Passed = LEFT(UPPER(actual), LEN(prefix)) = UPPER(prefix)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Starts with '" & prefix & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: 531548b3-b3a7-40aa-94a0-e56e431f6a67

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a string ends with the specified suffix (case-insensitive).
function 'PQL.Assert.ShouldEndWith' =
		(testName : STRING, suffix : STRING, actual : STRING) =>
			VAR _Passed = RIGHT(UPPER(actual), LEN(suffix)) = UPPER(suffix)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Ends with '" & suffix & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: 22c34281-bf85-4603-9a7e-c9eeec359b5e

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a string contains the specified substring (case-insensitive).
function 'PQL.Assert.ShouldContainString' =
		(testName : STRING, substring : STRING, actual : STRING) =>
			VAR _Passed = CONTAINSSTRING(actual, substring)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Contains '" & substring & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: b98e7a80-3474-4a85-9d52-bba73c7b15d8

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a string matches a pattern (case-insensitive).
function 'PQL.Assert.ShouldMatch' =
		(testName : STRING, pattern : STRING, actual : STRING) =>
			VAR _Passed = CONTAINSSTRING(actual, pattern)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Matches '" & pattern & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: b796a564-b4ca-47f5-a945-e992449dc778

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Col Assertions - PQL.Assert.Col namespace
/// Asserts that all values in a column are NULL (DAX BLANK()).
function 'PQL.Assert.Col.ShouldBeNull' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonNullCount = DISTINCTCOUNTNOBLANK(columnRef)
			VAR _Passed = _NonNullCount = 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all NULL",
				"Actual", IF(_Passed, "ALL NULL", "HAS NON-NULL VALUES (" & FORMAT(_NonNullCount, "0") & " distinct)"),
				"Passed", _Passed
			)
	lineageTag: 80ccaf27-70f7-4890-8db3-f96fe65568d7

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a column contains at least one non-NULL value (not DAX BLANK()).
function 'PQL.Assert.Col.ShouldNotBeNull' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonNullCount = DISTINCTCOUNTNOBLANK(columnRef)
			VAR _Passed = _NonNullCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-NULL values",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonNullCount, "0") & " distinct)", "ALL NULL"),
				"Passed", _Passed
			)
	lineageTag: 54843639-b5e0-4f2b-927e-33649376fb37

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that all values in a column are BLANK (empty string "").
function 'PQL.Assert.Col.ShouldBeBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _BlankCount = COUNTAX(
				VALUES(columnRef),
				IF(FORMAT(columnRef, "") = "" && NOT ISBLANK(columnRef), 1, BLANK())
			)
			VAR _TotalCount = COUNTROWS(VALUES(columnRef))
			VAR _Passed = _BlankCount = _TotalCount
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all BLANK (empty string)",
				"Actual", IF(_Passed, "ALL BLANK", "HAS NON-BLANK VALUES (" & FORMAT(_TotalCount - _BlankCount, "0") & " non-blank)"),
				"Passed", _Passed
			)
	lineageTag: dd9f2344-1fd3-48a7-bc5d-eff4cfdfa4e8

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a column contains at least one non-BLANK value (not empty string "").
function 'PQL.Assert.Col.ShouldNotBeBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonBlankCount = COUNTAX(
				VALUES(columnRef),
				IF(FORMAT(columnRef, "") <> "", 1, BLANK())
			)
			VAR _Passed = _NonBlankCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-BLANK values (not empty string)",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonBlankCount, "0") & " non-blank)", "ALL BLANK"),
				"Passed", _Passed
			)
	lineageTag: 0c540c7a-4c79-4777-86ca-168a6e2b565d

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that all values in a column are NULL or BLANK (BLANK() or empty string "").
function 'PQL.Assert.Col.ShouldBeNullOrBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonEmptyCount = COUNTAX(
				VALUES(columnRef),
				IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
			)
			VAR _Passed = _NonEmptyCount = 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all NULL or BLANK",
				"Actual", IF(_Passed, "ALL NULL/BLANK", "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)"),
				"Passed", _Passed
			)
	lineageTag: 0851a60d-237e-42d2-b0db-5bd73a1943f6

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a column contains at least one value that is not NULL and not BLANK.
function 'PQL.Assert.Col.ShouldNotBeNullOrBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonEmptyCount = COUNTAX(
				VALUES(columnRef),
				IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
			)
			VAR _Passed = _NonEmptyCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-NULL, non-BLANK values",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)", "ALL NULL/BLANK"),
				"Passed", _Passed
			)
	lineageTag: 3b6d19c4-83e3-4557-ac15-adebf9c4a8b8

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that all values in a column are unique (no duplicates).
function 'PQL.Assert.Col.ShouldBeDistinct' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _DistinctCount = DISTINCTCOUNT(columnRef)
			VAR _TotalCount = COUNTROWS(VALUES(columnRef))
			VAR _Passed = _DistinctCount = _TotalCount
			RETURN ROW(
				"TestName", testName,
				"Expected", "All values distinct",
				"Actual", IF(_Passed, "ALL DISTINCT (" & FORMAT(_TotalCount, "0") & " rows)", "DUPLICATES FOUND (" & FORMAT(_DistinctCount, "0") & " distinct of " & FORMAT(_TotalCount, "0") & " rows)"),
				"Passed", _Passed
			)
	lineageTag: c0dfca6c-e046-4d1b-bbb7-1806e0e2d0dc

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.Col.ShouldExist' =
		(testName : STRING, tableName : STRING, columnName : STRING) =>
			VAR _Columns = INFO.VIEW.COLUMNS()
			VAR _Found = COUNTROWS(FILTER(_Columns, [Table] = tableName && [Name] = columnName)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column '" & tableName & "[" & columnName & "]' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: 0602da22-8e81-4ce2-88d8-8b7045a5abec

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Table Assertions - PQL.Assert.Table namespace
/// Asserts that a table has at least one row.
function 'PQL.Assert.Tbl.ShouldHaveRows' =
		(testName : STRING, tableRef : TABLE EXPR) =>
			VAR _RowCount = COUNTROWS(tableRef)
			VAR _Passed = _RowCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Table has rows",
				"Actual", IF(_Passed, "HAS ROWS (" & FORMAT(_RowCount, "0") & ")", "EMPTY (0 rows)"),
				"Passed", _Passed
			)
	lineageTag: 4b02a1c6-7c7d-4d5a-8ac6-5958754a6db4

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Asserts that a table has exactly the expected number of rows.
function 'PQL.Assert.Tbl.ShouldHaveRowCount' =
		(testName : STRING, tableRef : TABLE EXPR, expectedRowCount : INT64) =>
			VAR _RowCount = COUNTROWS(tableRef)
			VAR _Passed = _RowCount = expectedRowCount
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(expectedRowCount, "0") & " rows",
				"Actual", FORMAT(_RowCount, "0") & " rows",
				"Passed", _Passed
			)
	lineageTag: 5e31df3b-4f6b-405c-a933-c19e31ad6bb1

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.Tbl.ShouldHaveMoreRowsThan' =
		(testName : STRING, threshold : INT64, tableToCheck : TABLE) =>
			VAR _RowCount = COUNTROWS(tableToCheck)
			VAR _Passed = _RowCount > threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "> " & FORMAT(threshold, "0"),
				"Actual", FORMAT(_RowCount, "0"),
				"Passed", _Passed
			)
	lineageTag: 6bf94dcd-4389-4fd2-9b31-eb5027ff06be

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'PQL.Assert.Tbl.ShouldExist' =
		(testName : STRING, tableName : STRING) =>
			VAR _Tables = INFO.VIEW.TABLES()
			VAR _Found = COUNTROWS(FILTER(_Tables, [Name] = tableName)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Table '" & tableName & "' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: f453612c-48d7-47e2-bd85-719e1c55271f

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Relationship Assertions - PQL.Assert.Relationship namespace
function 'PQL.Assert.Relationship.ShouldExist' =
		(testName : STRING, fromTable : STRING, fromColumn : STRING, toTable : STRING, toColumn : STRING) =>
			VAR _Relationships = INFO.VIEW.RELATIONSHIPS()
			VAR _Found = COUNTROWS(FILTER(_Relationships,
				[FromTable] = fromTable && [FromColumn] = fromColumn &&
				[ToTable] = toTable && [ToColumn] = toColumn)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Relationship '" & fromTable & "[" & fromColumn & "] -> " & toTable & "[" & toColumn & "]' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: 86cccc8d-877b-40a3-80b7-04b01fc5971d

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Test Discovery Functions - PQL.Assert namespace
function 'PQL.Assert.RetrieveTests' =
		() =>
			FILTER(INFO.FUNCTIONS("ORIGIN", "2"), OR(RIGHT([FUNCTION_NAME],5) = ".Test", RIGHT([FUNCTION_NAME],6) = ".Tests"))
	lineageTag: 1184ecd8-2cd7-45c5-bed5-aa5925588788

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

/// Retrieves tests filtered by environment. Looks for .{ENV}. or .ANY. in function names.
/// If environment is blank, returns all tests via PQL.Assert.RetrieveTests().
/// Supports any custom environment naming convention (DEV, TEST, PROD, UAT, STAGING, etc.)
function 'PQL.Assert.RetrieveTestsByEnvironment' =
		(environment: STRING) =>
			VAR _EnvUpper = UPPER(environment)
			RETURN
				FILTER(
					INFO.FUNCTIONS("ORIGIN", "2"),
					(RIGHT([FUNCTION_NAME], 5) = ".Test" || RIGHT([FUNCTION_NAME], 6) = ".Tests")
					&& (
						ISBLANK(_EnvUpper)
						|| CONTAINSSTRING(UPPER([FUNCTION_NAME]), "." & _EnvUpper & ".")
						|| CONTAINSSTRING(UPPER([FUNCTION_NAME]), ".ANY.")
					)
				)
	lineageTag: deae0259-0871-4b9a-9639-958dfe2f5350

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.3

function 'Example.prod.test' =
		() =>
				UNION (
					PQL.Assert.ShouldNotBeNull("ShouldNotBeNull: DISTINCTCOUNTNOBLANK expression should pass", DISTINCTCOUNTNOBLANK('TestData'[ID])),
					PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: ID has values", 'TestData'[ID]),
					PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: NullableField should have values", 'TestData'[NullableField])
				)
	lineageTag: ad2608c4-4c55-4fbc-bdf3-b412bd6fe00d

function 'ShouldFailSchema.DEV.Tests' =
		() =>
			DATATABLE("Test",TEXT,{{"HI"}})
	lineageTag: e1d24d78-a5c5-4895-bdc4-b1d2f5e02be8

function 'ShouldFail.TEST.Tests' =
		() =>
			PQL.Assert.ShouldBeLessThan("Failed Test",4,4)
	lineageTag: 9ad3077c-9087-4b1f-a310-a5b73f8c93df

function 'Tbl.DEV.Tests' =
		() =>
			PQL.Assert.Tbl.ShouldHaveMoreRowsThan("Test Date should have more rows that 1",1,TestData)
	lineageTag: 4f100628-2e92-435a-8b83-a0b0e9243f2b

function 'Example.ANY.Tests' =
		() =>
			UNION (
				PQL.Assert.ShouldNotBeNull("ShouldNotBeNull: DISTINCTCOUNTNOBLANK expression should pass", DISTINCTCOUNTNOBLANK('TestData'[ID])),
				PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: ID has values", 'TestData'[ID]),
				PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: NullableField should have values", 'TestData'[NullableField])
			)
	lineageTag: 05c02d7e-b93e-4810-8c4c-0e6e59cb6f85

function 'Example.MYenv.Tests' =
		() =>
				UNION (
					PQL.Assert.ShouldNotBeNull("ShouldNotBeNull: DISTINCTCOUNTNOBLANK expression should pass", DISTINCTCOUNTNOBLANK('TestData'[ID])),
					PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: ID has values", 'TestData'[ID]),
					PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: NullableField should have values", 'TestData'[NullableField])
				)
	lineageTag: 1a735110-499e-4100-bb5c-1c6ec52f6d02

