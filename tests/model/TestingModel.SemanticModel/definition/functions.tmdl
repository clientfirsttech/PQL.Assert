/// DAX Assertion Library Functions
/// Publisher: PQL (Power Query Lint)
/// These functions provide a standardized way to write unit tests for DAX models
/// Basic Assertions - PQL.Assert namespace
function 'PQL.Assert.ShouldBeTrue' =
		(testName : STRING, actualCondition : BOOLEAN) =>
			VAR _Passed = actualCondition = TRUE
			RETURN ROW(
				"TestName", testName,
				"Expected", "TRUE",
				"Actual", IF(actualCondition, "TRUE", "FALSE"),
				"Passed", _Passed
			)
	lineageTag: 2b17b534-c064-47a4-b443-05e59bdeae8e

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldBeFalse' =
		(testName : STRING, actualCondition : BOOLEAN) =>
			VAR _Passed = actualCondition = FALSE
			RETURN ROW(
				"TestName", testName,
				"Expected", "FALSE",
				"Actual", IF(actualCondition, "TRUE", "FALSE"),
				"Passed", _Passed
			)
	lineageTag: 04919670-faad-4937-8a6c-911f802cd855

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is NULL (DAX BLANK()).
function 'PQL.Assert.ShouldBeNull' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = ISBLANK(actualValue)
			RETURN ROW(
				"TestName", testName,
				"Expected", "NULL",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 9d323be5-467d-4f57-939d-c9e40b284f09

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is not NULL (not DAX BLANK()). Accepts both simple values and expressions.
function 'PQL.Assert.ShouldNotBeNull' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = NOT ISBLANK(_ActualResult)
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT NULL",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: 3c53ffba-8b87-4381-854d-ed8ee86a5594

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is BLANK (empty string "").
function 'PQL.Assert.ShouldBeBlank' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = NOT ISBLANK(actualValue) && actualValue = ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "BLANK (empty string)",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: bb1c5348-5a6c-40fb-81e1-301daf761ed1

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is not BLANK (not empty string "").
function 'PQL.Assert.ShouldNotBeBlank' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = ISBLANK(_ActualResult) || _ActualResult <> ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT BLANK (not empty string)",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: c255407e-edaf-4a62-bbba-2e693d09bca5

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is NULL or BLANK (BLANK() or empty string "").
function 'PQL.Assert.ShouldBeNullOrBlank' =
		(testName : STRING, actualValue : VARIANT) =>
			VAR _Passed = ISBLANK(actualValue) || actualValue = ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NULL OR BLANK",
				"Actual", IF(ISBLANK(actualValue), "NULL", IF(actualValue = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: d9d1aa9b-fe20-476c-b996-af29eb1da179

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a value is not NULL and not BLANK (has actual content).
function 'PQL.Assert.ShouldNotBeNullOrBlank' =
		(testName : STRING, actualValue : SCALAR VARIANT EXPR) =>
			VAR _ActualResult = actualValue
			VAR _Passed = NOT ISBLANK(_ActualResult) && _ActualResult <> ""
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT NULL AND NOT BLANK",
				"Actual", IF(ISBLANK(_ActualResult), "NULL", IF(_ActualResult = "", "BLANK (empty string)", "HAS VALUE")),
				"Passed", _Passed
			)
	lineageTag: cbf87044-29b3-4047-a95e-94aa4cafa4cd

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldEqual' =
		(testName : STRING, expected : VARIANT, actual : VARIANT) =>
			VAR _Passed = expected = actual
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(expected, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 0a233078-fc41-4473-9161-aa825dce1825

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldNotEqual' =
		(testName : STRING, notExpected : VARIANT, actual : VARIANT) =>
			VAR _Passed = notExpected <> actual
			RETURN ROW(
				"TestName", testName,
				"Expected", "NOT " & FORMAT(notExpected, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 2952a4cf-adcd-457e-9206-dcf8f09bd22f

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that two strings are exactly equal (case-sensitive comparison).
function 'PQL.Assert.ShouldEqualExactly' =
		(testName : STRING, expected : STRING, actual : STRING) =>
			VAR _Passed = EXACT(expected, actual)
			RETURN ROW(
				"TestName", testName,
				"Expected", expected,
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: a6f5c784-78ea-4d2e-8626-f52108dddcd1

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldBeGreaterThan' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual > threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "> " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: b2f8d0ed-481d-4a92-9695-a85ddcb06381

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldBeLessThan' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual < threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "< " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 6171f3d2-3b19-412a-ba95-c533dbe43384

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a number is greater than or equal to an expected value.
function 'PQL.Assert.ShouldBeGreaterOrEqual' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual >= threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", ">= " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: fa1eabd1-2c90-43d7-9977-5c45bb1a0aeb

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a number is less than or equal to an expected value.
function 'PQL.Assert.ShouldBeLessOrEqual' =
		(testName : STRING, threshold : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual <= threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "<= " & FORMAT(threshold, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 8588a9c4-1fa4-4864-a755-b445431fa5e7

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.ShouldBeBetween' =
		(testName : STRING, lowerBound : DOUBLE, upperBound : DOUBLE, actual : DOUBLE) =>
			VAR _Passed = actual >= lowerBound && actual <= upperBound
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(lowerBound, "") & " <= value <= " & FORMAT(upperBound, ""),
				"Actual", FORMAT(actual, ""),
				"Passed", _Passed
			)
	lineageTag: 46cf34d0-ee97-444d-9390-19a9b01ac6d7

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// String Assertions - PQL.Assert namespace
/// Asserts that a string starts with the specified prefix (case-insensitive).
function 'PQL.Assert.ShouldStartWith' =
		(testName : STRING, prefix : STRING, actual : STRING) =>
			VAR _Passed = LEFT(UPPER(actual), LEN(prefix)) = UPPER(prefix)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Starts with '" & prefix & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: fc6bbd37-0acb-41f4-b76c-8bf71d76a933

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a string ends with the specified suffix (case-insensitive).
function 'PQL.Assert.ShouldEndWith' =
		(testName : STRING, suffix : STRING, actual : STRING) =>
			VAR _Passed = RIGHT(UPPER(actual), LEN(suffix)) = UPPER(suffix)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Ends with '" & suffix & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: ee64fde3-ffed-498c-909f-ea9178292f8f

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a string contains the specified substring (case-insensitive).
function 'PQL.Assert.ShouldContainString' =
		(testName : STRING, substring : STRING, actual : STRING) =>
			VAR _Passed = CONTAINSSTRING(actual, substring)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Contains '" & substring & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: 6cdb717b-fe4c-4b26-ad09-8da5392d9214

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a string matches a pattern (case-insensitive).
function 'PQL.Assert.ShouldMatch' =
		(testName : STRING, pattern : STRING, actual : STRING) =>
			VAR _Passed = CONTAINSSTRING(actual, pattern)
			RETURN ROW(
				"TestName", testName,
				"Expected", "Matches '" & pattern & "'",
				"Actual", actual,
				"Passed", _Passed
			)
	lineageTag: bf1e2dfc-c6dd-439c-9e35-78cee80904cf

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Col Assertions - PQL.Assert.Col namespace
/// Asserts that all values in a column are NULL (DAX BLANK()).
function 'PQL.Assert.Col.ShouldBeNull' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonNullCount = DISTINCTCOUNTNOBLANK(columnRef)
			VAR _Passed = _NonNullCount = 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all NULL",
				"Actual", IF(_Passed, "ALL NULL", "HAS NON-NULL VALUES (" & FORMAT(_NonNullCount, "0") & " distinct)"),
				"Passed", _Passed
			)
	lineageTag: e23377c2-357c-4a69-a71b-a83634d70f5a

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a column contains at least one non-NULL value (not DAX BLANK()).
function 'PQL.Assert.Col.ShouldNotBeNull' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonNullCount = DISTINCTCOUNTNOBLANK(columnRef)
			VAR _Passed = _NonNullCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-NULL values",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonNullCount, "0") & " distinct)", "ALL NULL"),
				"Passed", _Passed
			)
	lineageTag: 5b52ad79-9353-43a4-8dca-3f00a929e2a9

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that all values in a column are BLANK (empty string "").
function 'PQL.Assert.Col.ShouldBeBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _BlankCount = COUNTAX(
				VALUES(columnRef),
				IF(FORMAT(columnRef, "") = "" && NOT ISBLANK(columnRef), 1, BLANK())
			)
			VAR _TotalCount = COUNTROWS(VALUES(columnRef))
			VAR _Passed = _BlankCount = _TotalCount
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all BLANK (empty string)",
				"Actual", IF(_Passed, "ALL BLANK", "HAS NON-BLANK VALUES (" & FORMAT(_TotalCount - _BlankCount, "0") & " non-blank)"),
				"Passed", _Passed
			)
	lineageTag: ca02256a-21c6-40c5-88e4-6538223d1555

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a column contains at least one non-BLANK value (not empty string "").
function 'PQL.Assert.Col.ShouldNotBeBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonBlankCount = COUNTAX(
				VALUES(columnRef),
				IF(FORMAT(columnRef, "") <> "", 1, BLANK())
			)
			VAR _Passed = _NonBlankCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-BLANK values (not empty string)",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonBlankCount, "0") & " non-blank)", "ALL BLANK"),
				"Passed", _Passed
			)
	lineageTag: dd7c3af9-c9f3-4e25-b38d-0ddfa41fccc7

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that all values in a column are NULL or BLANK (BLANK() or empty string "").
function 'PQL.Assert.Col.ShouldBeNullOrBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonEmptyCount = COUNTAX(
				VALUES(columnRef),
				IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
			)
			VAR _Passed = _NonEmptyCount = 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column is all NULL or BLANK",
				"Actual", IF(_Passed, "ALL NULL/BLANK", "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)"),
				"Passed", _Passed
			)
	lineageTag: 1e579278-1f19-46db-9b2e-a83d6583704a

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a column contains at least one value that is not NULL and not BLANK.
function 'PQL.Assert.Col.ShouldNotBeNullOrBlank' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _NonEmptyCount = COUNTAX(
				VALUES(columnRef),
				IF(ISBLANK(columnRef) || FORMAT(columnRef, "") = "", BLANK(), 1)
			)
			VAR _Passed = _NonEmptyCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column has non-NULL, non-BLANK values",
				"Actual", IF(_Passed, "HAS VALUES (" & FORMAT(_NonEmptyCount, "0") & " non-empty)", "ALL NULL/BLANK"),
				"Passed", _Passed
			)
	lineageTag: 0c2c5e5d-55c6-43ef-bc0a-ea941202e200

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that all values in a column are unique (no duplicates).
function 'PQL.Assert.Col.ShouldBeDistinct' =
		(testName : STRING, columnRef : ANYREF EXPR) =>
			VAR _DistinctCount = DISTINCTCOUNT(columnRef)
			VAR _TotalCount = COUNTROWS(VALUES(columnRef))
			VAR _Passed = _DistinctCount = _TotalCount
			RETURN ROW(
				"TestName", testName,
				"Expected", "All values distinct",
				"Actual", IF(_Passed, "ALL DISTINCT (" & FORMAT(_TotalCount, "0") & " rows)", "DUPLICATES FOUND (" & FORMAT(_DistinctCount, "0") & " distinct of " & FORMAT(_TotalCount, "0") & " rows)"),
				"Passed", _Passed
			)
	lineageTag: a9bac1aa-3fbd-421c-a5ea-29e7f5b00dd8

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.Col.ShouldExist' =
		(testName : STRING, tableName : STRING, columnName : STRING) =>
			VAR _Columns = INFO.VIEW.COLUMNS()
			VAR _Found = COUNTROWS(FILTER(_Columns, [Table] = tableName && [Name] = columnName)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Column '" & tableName & "[" & columnName & "]' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: c114ed82-8827-42d3-9266-27060b9dc642

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Table Assertions - PQL.Assert.Table namespace
/// Asserts that a table has at least one row.
function 'PQL.Assert.Tbl.ShouldHaveRows' =
		(testName : STRING, tableRef : TABLE EXPR) =>
			VAR _RowCount = COUNTROWS(tableRef)
			VAR _Passed = _RowCount > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Table has rows",
				"Actual", IF(_Passed, "HAS ROWS (" & FORMAT(_RowCount, "0") & ")", "EMPTY (0 rows)"),
				"Passed", _Passed
			)
	lineageTag: a662c65a-d24c-4301-b287-109247184992

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Asserts that a table has exactly the expected number of rows.
function 'PQL.Assert.Tbl.ShouldHaveRowCount' =
		(testName : STRING, tableRef : TABLE EXPR, expectedRowCount : INT64) =>
			VAR _RowCount = COUNTROWS(tableRef)
			VAR _Passed = _RowCount = expectedRowCount
			RETURN ROW(
				"TestName", testName,
				"Expected", FORMAT(expectedRowCount, "0") & " rows",
				"Actual", FORMAT(_RowCount, "0") & " rows",
				"Passed", _Passed
			)
	lineageTag: 8e2f1733-4f65-4f5a-b791-f4ba362cab49

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.Tbl.ShouldHaveMoreRowsThan' =
		(testName : STRING, threshold : INT64, tableToCheck : TABLE) =>
			VAR _RowCount = COUNTROWS(tableToCheck)
			VAR _Passed = _RowCount > threshold
			RETURN ROW(
				"TestName", testName,
				"Expected", "> " & FORMAT(threshold, "0"),
				"Actual", FORMAT(_RowCount, "0"),
				"Passed", _Passed
			)
	lineageTag: b4524b56-3307-4768-a7de-aacca665ca49

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'PQL.Assert.Tbl.ShouldExist' =
		(testName : STRING, tableName : STRING) =>
			VAR _Tables = INFO.VIEW.TABLES()
			VAR _Found = COUNTROWS(FILTER(_Tables, [Name] = tableName)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Table '" & tableName & "' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: 4ef014c1-f95e-434c-a5f3-21019816595f

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Relationship Assertions - PQL.Assert.Relationship namespace
function 'PQL.Assert.Relationship.ShouldExist' =
		(testName : STRING, fromTable : STRING, fromColumn : STRING, toTable : STRING, toColumn : STRING) =>
			VAR _Relationships = INFO.VIEW.RELATIONSHIPS()
			VAR _Found = COUNTROWS(FILTER(_Relationships,
				[FromTable] = fromTable && [FromColumn] = fromColumn &&
				[ToTable] = toTable && [ToColumn] = toColumn)) > 0
			RETURN ROW(
				"TestName", testName,
				"Expected", "Relationship '" & fromTable & "[" & fromColumn & "] -> " & toTable & "[" & toColumn & "]' exists",
				"Actual", IF(_Found, "EXISTS", "NOT FOUND"),
				"Passed", _Found
			)
	lineageTag: 2eda02c1-7c38-4bb6-a72e-938ae6c71582

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

/// Test Discovery Functions - PQL.Assert namespace
function 'PQL.Assert.RetrieveTests' =
		() =>
			FILTER(INFO.FUNCTIONS("ORIGIN", "2"), OR(RIGHT([FUNCTION_NAME],5) = ".Test", RIGHT([FUNCTION_NAME],6) = ".Tests"))
	lineageTag: 864e7cff-10e4-4697-a8ec-6ae4454cda9d

	annotation DAXLIB_PackageId = PQL.Assert

	annotation DAXLIB_PackageVersion = 0.1.0

function 'Example.Tests' =
		() =>
			UNION (
				PQL.Assert.ShouldNotBeNull("ShouldNotBeNull: DISTINCTCOUNTNOBLANK expression should pass", DISTINCTCOUNTNOBLANK('TestData'[ID])),
				PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: ID has values", 'TestData'[ID]),
				PQL.Assert.Col.ShouldNotBeNull("Col.ShouldNotBeNull: NullableField should have values", 'TestData'[NullableField])
			)
	lineageTag: 93b20ae2-c4e1-4824-95c9-61f012a2e137

function 'Tbl.Tests' =
		() =>
			PQL.Assert.Tbl.ShouldHaveMoreRowsThan("Test Date should have more rows that 1",1,TestData)
	lineageTag: fc38c159-69cf-416c-a266-4818f95a26e8

